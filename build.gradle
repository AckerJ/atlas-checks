plugins {
	id 'java'
	id 'maven'
	id 'maven-publish'
	id 'idea'
	id 'eclipse'
	id 'signing'
	id 'checkstyle'
	id 'jacoco'
	id "com.diffplug.gradle.spotless" version "3.27.0"
	id 'org.sonarqube' version '2.8'
	// id "io.codearte.nexus-staging" version "0.12.0"
	id 'com.github.johnrengelman.shadow' version '5.0.0'
}

apply from: 'dependencies.gradle'
apply from: 'gradle/quality.gradle'
apply from: 'gradle/deployment.gradle'
apply from: 'gradle/execute.gradle'

description = "Atlas Checks"

sourceCompatibility = 11
targetCompatibility = 11

repositories
{
    // For geotools
    maven {
      url "http://repo.osgeo.org/repository/release/"
      content {
        // osgeo removed the jar and added a -norce version
        excludeVersion("log4j", "log4j", "1.2.17")
      }
    }
    mavenCentral()
    // For Spark CDH
    maven { url "https://repository.cloudera.com/content/repositories/releases/" }
    // For jetty (through spark)
    maven { url "http://repository.cloudera.com/cloudera/cloudera-repos/" }
    // For staged repositories (before they get to maven central)
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
}

configurations
{
    compile
    {
        resolutionStrategy
        {
            force packages.atlas
            force packages.atlas_generator

            // Snappy 1.1.1.6 is the one that has the proper .so libs.
            // https://github.com/xerial/snappy-java/issues/6
            force 'org.xerial.snappy:snappy-java:1.1.1.6'
        }
    }
    shadow
    {
        resolutionStrategy
        {
            force packages.atlas
            force packages.atlas_generator
        }
    }
    runtime
    archives
}

dependencies
{
    compile packages.commons
    compile packages.atlas
    compile packages.atlas_generator
    compile packages.postgis
    compile packages.postgres
    compile packages.spring
    compile packages.mockito
    compile packages.log4j
    compile packages.sqlite

    checkstyle packages.checkstyle
    checkstyle packages.atlas_checkstyle

    shadow project.configurations.getByName('compile')

    // Support Junit 5 tests
    testImplementation packages.junit.api
    testImplementation packages.junit.params
    testRuntimeOnly packages.junit.engine
    // Support JUnit 3/4 tests
    testCompileOnly packages.junit.junit4
    testRuntimeOnly packages.junit.vintage
}

/**
 * Artifact related items
 */
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

import com.github.jengelman.gradle.plugins.shadow.transformers.AppendingTransformer
import com.github.jengelman.gradle.plugins.shadow.transformers.ServiceFileTransformer

shadowJar {
    baseName = project.name
    configurations = [project.configurations.shadow]
    transform(AppendingTransformer) {
        resource = 'reference.conf'
    }
    transform(ServiceFileTransformer)
    zip64 = true
}

artifacts
{
    archives javadocJar
    archives sourcesJar
    archives shadowJar
}

/*
 * This is to skip the tasks for which there is a skip<TaskName>=true
 * environment variable
 */
def skippedTaskNames = System.getenv().findAll { key, value ->
    key.startsWith("skip") && value.equalsIgnoreCase("true")
}.keySet().collect { it.substring(4) }
gradle.startParameter.excludedTaskNames += skippedTaskNames

idea {
    project {
        languageLevel = '11'
    }
}
